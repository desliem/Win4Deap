/***********************************************************************************************/
/* Macro that generates a WinDeap file with .ded extension from a SAS data set.                */
/* First, the path where the WinDeap file will be generated should be specify (%let path=).    */
/* Then, input parameters to specify in the macro are:                                         */
/*    table: SAS data set that contains DMUs, periods, outputs and inputs variables            */
/*    ndmu: number of DMUs                                                                     */
/*    nper: number of periods                                                                  */
/*    nout: number of outputs                                                                  */
/*    nin: number of inputs                                                                    */
/*    price: 0=no price information for the inputs and 1=price information for the inputs      */
/*    orientation: 0=input and 1=output orientated                                             */
/*    returns: 0=constant and 1=variable returns to scale                                      */
/*    calculate: 0=DEA(1-stage), 1=DEA(2-stage), 2=DEA(multi-stage), 3=DEA-COST, 4=MALMQUIST   */
/* In the data file, the variable DMU should contains DMUs names (DMU1,...,DMUn), the variable */
/* Period contains the periods numbers, then the outputs are listed, followed by the inputs    */
/* columns (left to right across the file). If you choose the cost efficiencies option you     */
/* will also need to supply price information for the inputs. These price columns should be    */
/* listed to the right of the input data columns and appear in the same order.                 */
/* Author: Julien CICCONE, CRP Henri Tudor, February 2005, julien.ciccone@tudor.lu             */
/***********************************************************************************************/




%macro WinDeap(table,ndmu,nper,nout,nin,price,orientation,returns,calculate);
data &table;
set &table;
order=substr(DMU,4)*1;
run;

proc sort data=&table;
by order DMU Period;
run;

proc transpose data=&table out=&table.Tra;
by order DMU Period;
run;

data _null_;
set &table.Tra end=eof;
if eof then call symput('nbobs',trim(left(put(_n_,8.))));
run;

%let date=%sysfunc(datetime(),DATETIME.);

data _null_;
set &table.Tra;
file "&path\&table..ded" lrecl=1000;
if _n_=1 then do;
put
"# File: &table..ded" '0D0A'x
"# Date: &date" '0D0A'x
"# Author: This file was generated by SAS" '0D0A'x
"# Note: Manual changes may make it unreadable." '0D0A'x
"VERSION 40" '0D0A'x
"MODEL" '0D0A'x
" ORIENTATION &orientation" '0D0A'x
" RETURNS &returns" '0D0A'x
" CALCULATE &calculate" '0D0A'x
" PERIODS &nper" '0D0A'x
"END" '0D0A'x
"DMUS" '0D0A'x
%do i=1 %to &ndmu;
   " DMU&i 1 1 dmu &i" '0D0A'x
%end;
"END" '0D0A'x
"COLS" '0D0A'x
%do j=1 %to &nout;
   " OUT&j 1 1 0 out &j" '0D0A'x
%end;
%do k=1 %to &nin;
   %if &price=0 %then " IN&k 0 1 0 in &k" '0D0A'x;
   %else %if &price=1 %then " IN&k 0 1 1 in &k" '0D0A'x;
%end;
"END" '0D0A'x
"DATA";
end;
put "  " COL1;
if _n_=&nbobs then do;
put
"END";
end;
run;
%mend WinDeap;




/* Example with 5 DMUs, 4 periods, 3 outputs and 2 inputs with price information */
data WinDeap;
input DMU $4. Period OUT1 OUT2 OUT3 IN1 INP1 IN2 INP2 best8.;
cards;
DMU1	0	1	2	3	4	5	6	7
DMU1	1	8	9	10	11	12	13	14
DMU1	2	15	16	17	18	19	20	21
DMU1	3	22	23	24	25	26	27	28
DMU2	0	29	30	31	32	33	34	35
DMU2	1	36	37	38	39	40	41	42
DMU2	2	43	44	45	46	47	48	49
DMU2	3	50	51	52	53	54	55	56
DMU3	0	57	58	59	60	61	62	63
DMU3	1	64	65	66	67	68	69	70
DMU3	2	71	72	73	74	75	76	77
DMU3	3	78	79	80	81	82	83	84
DMU4	0	85	86	87	88	89	90	91
DMU4	1	92	93	94	95	96	97	98
DMU4	2	99	100	101	102	103	104	105
DMU4	3	106	107	108	109	110	111	112
DMU5	0	113	114	115	116	117	118	119
DMU5	1	120	121	122	123	124	125	126
DMU5	2	127	128	129	130	131	132	133
DMU5	3	134	135	136	137	138	139	140
;
run;


%let path=G:\DATA\JCiccone\euklems\software\windeap\examples;
%WinDeap(WinDeap,5,4,3,2,1,1,0,4);
